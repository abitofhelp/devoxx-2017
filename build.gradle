buildscript {
	ext.kotlin_version = '1.1.51'

	repositories {
		jcenter()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
		classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
		classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE")
		classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
		classpath "com.github.jengelman.gradle.plugins:shadow:1.2.4"
		classpath "gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.7"
		classpath "com.github.ben-manes:gradle-versions-plugin:0.15.0"
		classpath "org.owasp:dependency-check-gradle:2.1.1"
		classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0-RC3"
		classpath "se.transmode.gradle:gradle-docker:1.2"
		classpath "gradle.plugin.com.github.ksoichiro:gradle-build-info-plugin:0.2.0"
		classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0-RC3"
	}
}

apply plugin: "idea"

idea {
	project {
		jdkName = '1.8'
		languageLevel = '8'
		vcs = 'Git'
	}
}

subprojects {
	apply plugin: "kotlin"
	apply plugin: "io.spring.dependency-management"
	apply plugin: "org.junit.platform.gradle.plugin"

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	configurations {
		detekt
		ktlint
	}

	repositories {
		jcenter()
	}

	dependencyManagement {
		imports {
			mavenBom "io.spring.platform:platform-bom:Brussels-SR5"
		}
	}

	dependencies {
		compile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
		compile "com.google.code.findbugs:jsr305:3.0.1"
		testCompile("org.junit.jupiter:junit-jupiter-api:5.0.0-RC2")
		testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0-RC2")
		testRuntime("org.junit.platform:junit-platform-launcher:1.0.0-RC2")
		testCompile("junit:junit:4.12")
		testRuntime("org.junit.vintage:junit-vintage-engine:4.12.0-RC2")
		testCompile "org.assertj:assertj-core:3.6.1"
		detekt 'io.gitlab.arturbosch.detekt:detekt-cli:1.0.0.RC5-3'
		ktlint "com.github.shyiko:ktlint:0.11.1"
	}

	task detekt(type: JavaExec, group: "verification") {
		description = "Run Detekt."
		main = "io.gitlab.arturbosch.detekt.cli.Main"
		classpath = configurations.detekt
		def input = "$projectDir"
		def config = "$rootDir/detekt.yml"
		def filters = ".*test.*"
		def params = [ '-i', input, '-c', config, '-f', filters]
		args(params)
	}

	task ktlint(type: JavaExec, group: "verification") {
		description = "Check Kotlin code style."
		main = "com.github.shyiko.ktlint.Main"
		classpath = configurations.ktlint
		args "src/**/*.kt"
	}
}

project(":main-partition") {
	apply plugin: "kotlin-spring"
	apply plugin: "org.springframework.boot"
	apply plugin: "docker"
	apply plugin: "com.github.ksoichiro.build.info"

	dependencies {
		compile project(":domain")
		compile project(":app-api")
		compile project(":app-impl")
		compile project(":infra-web")
		compile project(":infra-persistence")

		compile("org.springframework.boot:spring-boot-starter-web") {
			exclude module: "spring-boot-starter-tomcat"
		}
		compile "org.springframework.boot:spring-boot-starter-undertow"
		compile "org.springframework.boot:spring-boot-actuator"

		testCompile "org.springframework.boot:spring-boot-starter-test"
		testCompile "com.jayway.restassured:spring-mock-mvc:2.9.0"
	}

	springBoot {
		buildInfo()
	}

	task buildDocker(type: Docker, dependsOn: build, group: "build") {
		description = "Build Docker image."
		applicationName = "project-kali-server"
		dockerfile = file("Dockerfile")
		tag = "sourcedbvba/project-kali-server"
		doFirst {
			copy {
				from jar
				into "${stageDir}/target"
			}
		}
	}

	task buildDockerLatest(type: Docker, dependsOn: build, group: "build") {
		description = "Build Docker image."
		applicationName = "project-kali-server"
		dockerfile = file("Dockerfile")
		tagVersion = "latest"
		tag = "sourcedbvba/project-kali-server"
		doFirst {
			copy {
				from jar
				into "${stageDir}/target"
			}
		}
	}
}

project(":domain") {

}

project(":app-api") {

}

project(":app-impl") {
	dependencies {
		compile project(":app-api")
		compile project(":domain")
	}
}

project("infra-persistence") {
	dependencies {
		compile project(":domain")
	}
}

project("infra-web") {
	dependencies {
		compile project(":app-api")
	}
}
